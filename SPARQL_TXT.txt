PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX f: <http://www.semanticweb.org/cj/ontologies/2016/2/Ontology1456999826312.owl#>

# 1 Where are the film screenings that are scheduled on Saturday 15th August 2014?
SELECT ?film ?title ?date
WHERE { 
	?film rdf:type f:FilmScreeningProgramme .
	?film f:hasTitleP ?title .
	?film f:hasDate ?date .
	FILTER (?date > "2014-08-15T00:00:00"^^xsd:dateTime && 
		?date <= "2014-08-16T00:00:00"^^xsd:dateTime)
}

# 2 Which programme items feature both Charles Stross and David Clements?
SELECT ?programme ?title ?name1 ?name2
WHERE {
	?programme rdf:type f:Programme .
	?programme f:hasTitleP ?title .
	?programme f:hasPerson ?p1 .
	?programme f:hasPerson ?p2 .
	?p1 f:name ?name1 .
	?p2 f:name ?name2 .
	filter (?name1 = "Charles Stross"^^xsd:string) .
	filter (?name2 = "David Clements"^^xsd:string) .
}

# 3 Who are the moderators of the science track panel session?
SELECT ?moderator ?name ?panelSession ?panelTitle ?track ?trackTitle
WHERE { 
	?moderator rdf:type f:ModeratorPerson ;
	f:name ?name .
	?panelSession f:hasModerator ?moderator ;
	f:hasTitleP ?panelTitle ;
	f:inTrack ?track .
	?track f:hasTitleT "Science"^^xsd:string ;
	f:hasTitleT ?trackTitle
}ORDER BY ASC(?moderator)

# 4 How many people are participating in the academic track?
SELECT ?track ?trackTitle (COUNT(?participant) AS ?NumberOfPeople)
WHERE { 
	?track rdf:type f:Track .
	?programme f:inTrack ?track .
	?programme f:hasParticipant ?participant .
	?track f:hasTitleT ?trackTitle .
	?track f:hasTitleT "Academic"^^xsd:string . 
}
GROUP BY ?track ?trackTitle 

# 5 What is the earliest programme item in the politics track?
SELECT ?programme ?programmeTitle ?date
WHERE { 
	?programme rdf:type f:Programme .
	?programme f:inTrack ?track .
	?programme f:hasTitleP ?programmeTitle .
	?programme f:hasDate ?date .
	?track f:hasTitleT "Politics"^^xsd:string .
}ORDER BY ASC(?date)
LIMIT 1











